alpha = { 'a'..'z' | 'A'..'Z' }
digit = { '0'..'9' }
newline = _{ "\r"? ~ "\n" }

ident = { alpha ~ ("_" | "-" | alpha | digit )* }
command_name = { ident | path }
target_name = @{ ident ~ (" " ~ ident)* }
in_target = _{ "in target: " ~ target_name }
aside = { "(" ~ in_target ~ ")" }
command = { command_name ~ " " ~ arg_list }

toplevel_command = { command ~ (" " ~ aside)? ~ newline }
nested_command = { indentation ~ command ~ newline }

message_type = { "note" | "error" | "warning" }
word = { (alpha | digit | "_" | "-" | "." | "-" | "\\" | "/" | "(" | ")" | ":" | "=" | "+")+ }
string = { word ~ (" " ~ word)* }
message_contents = { string }
message = { message_type ~ ": " ~ message_contents }
message_line = _{ message ~ newline }
full_command = { toplevel_command ~ (nested_command | message_line)* ~ commenty_bits? ~ newline }

multiline_comment = ${ "/* com.apple.actool.compilation-results */"  }
comment_line = _{ multiline_comment ~ newline }
commenty_bits = { comment_line ~ (string ~ newline)+ ~ newline }

path_part = { ("_" | "-" | alpha | digit | "\\ " | "." | "-" | "+")* }
path = { ("/" ~ path_part)+ }
path_list = _{ path ~ (" " ~ path)* }
arg = { !aside ~ (path | string) }
arg_list = _{ arg ~ (" " ~ arg)* }
indentation = _{ " "{4} }

duration = @{ digit+ ~ "." ~ digit+ ~ " sec" }

build_interrupted = { "INTERRUPTED"}
build_succeeded = { "SUCCEEDED" }
build_failed = { "FAILED" }
build_outcome = { build_succeeded | build_failed | build_interrupted }
action_name = { "BUILD" | "CLEAN" }
build_status = {
    "** " ~ action_name ~ " " ~ build_outcome ~ " **" ~
    (" [" ~ duration ~ "]")?
}

action = _{ message_line* ~ (newline | full_command*) ~ build_status ~ newline* }
thing_we_care_about = _{
    newline* ~
    (message_line | full_command | build_status) ~
    newline* ~
    eoi
}
entire = _{ soi ~ newline* ~ action+ ~ eoi }
